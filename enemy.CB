Function LoadEnemyData(file$)
	If FileExists(file$)
		f = OpenToRead(file$)
			//K‰yd‰‰n koko tiedosto l‰pi
			While Not EOF(f)
				//Luetaan 'komento'
				command$ = Lower(Trim(ReadLine(f)))
				
				//Jos oli oikea komento tornin luomiseen
				If command$ = "[enemy]"
					nType.ENEMYTYPE = New(ENEMYTYPE)
					
					Repeat
						l$ = ReadLine(f)
						attr$ = Lower(Trim(GetWord(l$,1,"=")))
						value = Int(GetWord(l$,2,"="))
						
						Select attr$
							Case "typenum"
								nType\typeNum = value
							Case "maxhp"
								nType\maxHp = value
							Case "speed"
								nType\speed = value
							Case "reward"
								nType\reward = value
							Case "style"
								val$ = Lower(Trim(GetWord(l$,2,"=")))
								
								Select val$
									Case "rect"
										nType\style = STYLE_RECT
									Case "+"
										nType\style = STYLE_PLUS
									Case "x"
										nType\style = STYLE_X
									Case "circ"
										nType\style = STYLE_CIRCLE
								EndSelect
							Case "size"
								nType\size = value
							Case "clr"
								nType\clr = MakeMEMBlock(3)
								
								values$ = GetWord(l$,2,"=")
								
								PokeByte nType\clr,0,Int(GetWord(values$,1,","))
								PokeByte nType\clr,1,Int(GetWord(values$,2,","))
								PokeByte nType\clr,2,Int(GetWord(values$,3,","))
						EndSelect
					Until Lower(l$) = "[e"+"nd]"
					
					nType\id = ConvertToInteger(nType)
					
					gTest = nType\id
				EndIf
			Wend
		CloseFile f
	Else
		MakeError "File " + file$ + " n"+"ot found!"
		Return False
	EndIf
EndFunction

//Palauttaa vihollistyypin id:n
Function GetEnemyTypeId(typeNum)
	For iType.ENEMYTYPE = Each ENEMYTYPE
		If iType\typeNum = typeNum
			Return iType\id
		EndIf
	Next iType
	Return False
EndFunction

//Luo uuden vihollisen ensimm‰iselle waypointille
Function CreateEnemy(enemyT)
	eType.ENEMYTYPE = ConvertToType(enemyT)
	
	nEnemy.ENEMY = New(ENEMY)
		nEnemy\x = gWaypoints(1,WP_X) * GRID_SIZE + GRID_SIZE / 2
		nEnemy\y = gWaypoints(1,WP_Y) * GRID_SIZE + GRID_SIZE / 2
		
		nEnemy\dir = gWaypoints(1,WP_DIR)
		
		nEnemy\nextWaypoint = 2
		
		nEnemy\hp = eType\maxHp
		nEnemy\speed = eType\speed
		nEnemy\reward = eType\reward
		
		nEnemy\style = eType\style
		nEnemy\size = eType\size
		nEnemy\clr = eType\clr
		
		nEnemy\id = ConvertToInteger(nEnemy)
EndFunction

//Kertoo onko kyseinen vihollinen viel‰ olemassa
Function IsAlive(enemyId)
	For iEnemy.ENEMY = Each ENEMY
		If iEnemy\id = enemyId
			Return True
		EndIf
	Next iEnemy
	Return False
EndFunction

//P‰ivitt‰‰ viholliset
Function UpdateEnemies()
	For iEnemy.ENEMY = Each ENEMY
		If iEnemy\dir = DIR_UP
			xPlus = 0
			yPlus = -iEnemy\speed
		ElseIf iEnemy\dir = DIR_DOWN
			xPlus = 0
			yPlus = iEnemy\speed
		ElseIf iEnemy\dir = DIR_LEFT
			xPlus = -iEnemy\speed
			yPlus = 0
		ElseIf iEnemy\dir = DIR_RIGHT
			xPlus = iEnemy\speed
			yPlus = 0
		EndIf
		
		iEnemy\x = iEnemy\x + xPlus' * (gLoopTime / 1000)
		iEnemy\y = iEnemy\y + yPlus' * (gLoopTime / 1000)
		
		tmpX = gWaypoints(iEnemy\nextWaypoint,WP_X) * GRID_SIZE + GRID_SIZE / 2
		tmpY = gWaypoints(iEnemy\nextWaypoint,WP_Y) * GRID_SIZE + GRID_SIZE / 2
		
		If Distance(iEnemy\x,iEnemy\y,tmpX,tmpY) <= 3
			If iEnemy\nextWaypoint = gWaypointAmount
				Delete iEnemy
				gEnemyAmount = gEnemyAmount - 1
			Else
				iEnemy\x = tmpX
				iEnemy\y = tmpY
				
				iEnemy\dir = gWaypoints(iEnemy\nextWaypoint,WP_DIR)
				
				iEnemy\nextWaypoint = iEnemy\nextWaypoint + 1
			EndIf
		EndIf
	Next iEnemy
EndFunction

//Piirt‰‰ viholliset
Function DrawEnemies()
	For iEnemy.ENEMY = Each ENEMY
		tmpX = iEnemy\x - (iEnemy\size / 2)
		tmpY = iEnemy\y - (iEnemy\size / 2)
	
		Color PeekByte(iEnemy\clr,0),PeekByte(iEnemy\clr,1),PeekByte(iEnemy\clr,2)
		If iEnemy\style = STYLE_CIRCLE
			Circle tmpX,tmpY,iEnemy\size,1
		ElseIf iEnemy\style = STYLE_RECT
			Box tmpX,tmpY,iEnemy\size,iEnemy\size,1
		ElseIf iEnemy\style = STYLE_X
			tmpSize = iEnemy\size / 5
			
			For i = 0 To 4
				Box tmpX + i * tmpSize,tmpY + i * tmpSize,tmpSize,tmpSize,1
				Box (tmpX + (iEnemy\size - tmpSize)) - i * tmpSize,tmpY + i * tmpSize,tmpSize,tmpSize,1
			Next i
		ElseIf iEnemy\style = STYLE_PLUS
			tmpSize = iEnemy\size / 5
			
			For i = 0 To 4
				Box tmpX + 2 * tmpSize,tmpY + i * tmpSize,tmpSize,tmpSize,1
				Box tmpX + i * tmpSize,tmpY + 2 * tmpSize,tmpSize,tmpSize,1
			Next i
		EndIf
	Next iEnemy
EndFunction

//Lataa vihollisaallot
Function LoadWaves(file$)
	ClearArray OFF
	
	If FileExists(file$)
		f = OpenToRead(file$)
			While Not EOF(f)
				l$ = ReadLine(f)
				
				//Jos rivi‰ ei ole kommentoitu
				If Left(l$,1) <> "#"
					gGroupAmount = gGroupAmount + 1
					ReDim gEnemyGroups(gGroupAmount + 1,GROUP_PROPERTIES - 1)
					
					gEnemyGroups(gGroupAmount,GROUP_WAVE) = Int(GetWord(l$,1,","))
					gEnemyGroups(gGroupAmount,GROUP_TYPE) = Int(GetWord(l$,2,","))
					gEnemyGroups(gGroupAmount,GROUP_AMOUNT) = Int(GetWord(l$,3,","))
				EndIf
			Wend
		CloseFile f
		
		Return True
	Else
		MakeError "File " + file$ + " n"+"ot found!"
		Return False
	EndIf
EndFunction

//P‰ivitt‰‰ vihollisaallot
Function UpdateWaves()
	If gSpawned = False
		For i = 1 To gGroupAmount
			If i = gCurrentGroup
				gEnemyAmount = gEnemyAmount + gEnemyGroups(i,GROUP_AMOUNT)
				
				For k = 1 To gEnemyGroups(i,GROUP_AMOUNT)
					nSpawn.SPAWN = New(SPAWN)
						nSpawn\num = gEnemyGroups(i,GROUP_TYPE)
				Next k
				
				If gEnemyGroups(i + 1,GROUP_WAVE) = gCurrentWave
					gCurrentGroup = gCurrentGroup + 1
				Else
					gCurrentGroup = gCurrentGroup + 1
					gSpawned = True
					Exit
				EndIf
			EndIf
		Next i
	EndIf
	
	For iSpawn.SPAWN = Each SPAWN
		If Timer() > gLastSpawn + SPAWN_INTERVAL
			CreateEnemy(GetEnemyTypeId(iSpawn\num))
			
			gLastSpawn = Timer()
			
			Delete iSpawn
		EndIf
	Next iSpawn
EndFunction		