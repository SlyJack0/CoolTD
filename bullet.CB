//Luo uuden ammuksen
Function CreateBullet(x,y,ang,speed,damage,clr,special)
	nBullet.BULLET = New(BULLET)
		nBullet\x = x
		nBullet\y = y
		
		nBullet\ang = ang
		nBullet\speed = speed
		
		nBullet\damage = damage
		
		nBullet\clr = clr
		
		nBullet\special = special
EndFunction

//P‰ivitt‰‰ ammukset
Function UpdateBullets()
	For iBullet.BULLET = Each BULLET
		bAir = False
		
		iBullet\x = iBullet\x + Cos(iBullet\ang) * iBullet\speed * (gLoopTime / 1000)
		iBullet\y = iBullet\y - Sin(iBullet\ang) * iBullet\speed * (gLoopTime / 1000)
		
		If iBullet\x < 0 Or iBullet\x > gScreenW Or iBullet\y < 0 Or iBullet\y > gScreenH
			Delete iBullet
		Else
			//Tˆrm‰ys vihollisiin
			For iEnemy.ENEMY = Each ENEMY
				//Tarkastetaan onko vihollinen ilmassa
				eAir = IsAirEnemy(iEnemy\id)
				
				If Distance(iBullet\x,iBullet\y,iEnemy\x,iEnemy\y) <= iEnemy\size
					//Tarkistetaan onko ammuksessa erikoisominaisuuksia
					If iBullet\special <> 0 And iEnemy\cond = 0
						For i = 0 To MEMBlockSize(iBullet\special) - 2 Step 2
							tmpSpecial = PeekByte(iBullet\special,i)
							
							If tmpSpecial = SPECIAL_FREEZE Or tmpSpecial = SPECIAL_FIRE
								iEnemy\cond = tmpSpecial
								iEnemy\condStart = Timer()
								iEnemy\condTime = PeekByte(iBullet\special,i + 1) * 1000
							ElseIf tmpSpecial = SPECIAL_AIR
								bAir = True
							EndIf
						Next i
					EndIf
					
					If eAir = True And bAir = True	//Jos vihollinen ON ilmassa ja torni ampuu ilmaan
						iEnemy\hp = iEnemy\hp - iBullet\damage
					ElseIf eAir = False And bAir = False	//Jos molemmat maassa
						iEnemy\hp = iEnemy\hp - iBullet\damage
					EndIf
					
					If iEnemy\hp <= 0	//Jos vihollinen kuolee
						//Tarkistetaan onko vihollisessa erikoisominaisuuksia
						If iEnemy\special <> 0
							For i = 0 To MEMBlockSize(iEnemy\special) - 2 Step 2
								tmpSpecial = PeekByte(iEnemy\special,i)
								
								If tmpSpecial = SPECIAL_SPLIT //Vihollisen jakautuminen
									If iEnemy\dir = DIR_UP
										deltaX = 0
										deltaY = 15
									ElseIf iEnemy\dir = DIR_DOWN
										deltaX = 0
										deltaY = -15
									ElseIf iEnemy\dir = DIR_LEFT
										deltaX = 15
										deltaY = 0
									ElseIf iEnemy\dir = DIR_RIGHT
										deltaX = -15
										deltaY = 0
									EndIf
									
									typeNum = PeekByte(iEnemy\special,i + 1)
									
									CreateEnemy2(iEnemy\x + deltaX,iEnemy\y + deltaY,iEnemy\dir,iEnemy\nextWaypoint,GetEnemyTypeId(typeNum))
									CreateEnemy2(iEnemy\x + 2 * deltaX,iEnemy\y +  2 * deltaY,iEnemy\dir,iEnemy\nextWaypoint,GetEnemyTypeId(typeNum))
									
									gEnemyAmount = gEnemyAmount + 2
								ElseIf tmpSpecial = SPECIAL_TRANSFORM //Vihollisen muuttuminen
									CreateEnemy2(iEnemy\x,iEnemy\y,iEnemy\dir,iEnemy\nextWaypoint,GetEnemyTypeId(PeekByte(iEnemy\special,i + 1)))
									gEnemyAmount = gEnemyAmount + 1
								EndIf
							Next i
						EndIf
						
						//R‰j‰hdys jos efektit p‰‰ll‰
						If gSettings(SETTING_EFFECTS) = 1
							Explosion(iEnemy\x,iEnemy\y,30,60,300,iEnemy\clr)
						endif
						
						//Rahan lis‰ys
						gMoney = gMoney + iEnemy\reward
						
						Delete iEnemy
						gEnemyAmount = gEnemyAmount - 1
					EndIf
					
					Delete iBullet
					Exit
				EndIf
			Next iEnemy
		EndIf
	Next iBullet
EndFunction

//Piirt‰‰ ammukset
Function DrawBullets()
	For iBullet.BULLET = Each BULLET
		Color PeekByte(iBullet\clr,0),PeekByte(iBullet\clr,1),PeekByte(iBullet\clr,2)
		Circle iBullet\x - 1,iBullet\y - 1,2,1
	Next iBullet
EndFunction
