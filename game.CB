//Alustaa pelin ja lataa kaiken tarvittavan
Function InitGame()
	If FileExists("Data/settings.dat")
		LoadSettings()
	Else
		DefaultSettings()
	EndIf
	
	LoadTowerData("data/towers.ini")
	LoadEnemyData("data/enemies.ini")
	LoadWaves("data/waves.txt")
	
	gMoney = 300
	
	LoadPath(1)
EndFunction 

//Pääohjelma
Function Run()
	Repeat
		loopStart = Timer()
		
		SetWindow "Money: " + gMoney + "    Current wave: " + gCurrentWave + "/" + gWaveAmount + "    " + gLoopTime + "    Enemy count: " + gEnemyAmount
		
		mX = RoundDown(MouseX() / GRID_SIZE)
		mY = RoundDown(MouseY() / GRID_SIZE)
		
		If gPlaceTower = True
			Color 128,0,0
			Box mX * GRID_SIZE,mY * GRID_SIZE,GRID_SIZE,GRID_SIZE
			
			If MouseHit(1) And CheckTile(mX,mY) and gShowBuildMenu = False
				CreateTower(mX,mY,gSelectedTower)
			ElseIf MouseHit(2)
				gPlaceTower = False
				gSelectedTower = 0
			EndIf
		Else
			If MouseHit(1)
				gSelectedTower = GetTowerId(mX,mY)
			EndIf
		EndIf
		
		If KeyHit(cbKeyB)
			gShowBuildMenu = Not gShowBuildMenu
		ElseIf KeyHit(28)
			gSpawned = False
			gCurrentWave = gCurrentWave + 1
			
			If gCurrentWave > gWaveAmount
				End
			endif
		EndIf
		
		UpdateWaves()
		UpdateEnemies()
		UpdateTowers()
		UpdateBullets()
		
		DrawGrid()
		DrawPath()
		DrawEnemies()
		DrawTowers()
		DrawBullets()
		
		If gShowBuildMenu = True
			BuildMenu(0,0)
		EndIf
		
		If gSelectedTower <> 0 And gPlaceTower = False
			TowerInfo(gSelectedTower)
		endif
		
		DrawScreen
		
		gLoopTime = (Timer() - loopStart)
	Forever
EndFunction

//Asettaa oletusasetukset
Function DefaultSettings()
	gSettings(SETTING_MVOLUME) = 100
	gSettings(SETTING_SVOLUME) = 100
	gSettings(SETTING_EFFECTS) = 1
	
	SaveSettings()
EndFunction

//Tallentaa asetukset
Function SaveSettings()
	f = OpenToWrite("Data/settings.dat")
		For i = 0 To SETTINGS_AMOUNT - 1
			WriteByte f,gSettings(i)
		Next i
	CloseFile f
EndFunction

//Lataa asetukset
Function LoadSettings()
	f = OpenToRead("Data/settings.dat")
		For i = 0 To SETTINGS_AMOUNT - 1
			gSettings(i) = ReadByte(f)
		Next i
	CloseFile f
EndFunction