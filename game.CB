//Alustaa pelin ja lataa kaiken tarvittavan
Function InitGame()
	If FileExists("data/settings.dat")
		LoadSettings()
	Else
		DefaultSettings()
	EndIf
	
	If gTowerTypeAmount = 0
		LoadTowerData("data/towers.ini")
	EndIf
	
	If gEnemyTypeAmount = 0
		LoadEnemyData("data/enemies.ini")
	EndIf
	
	If gWaveAmount = 0
		LoadWaves("data/waves.txt")
	EndIf
	
	gCurrentWave = 0
	gCurrentGroup = 1
	gSpawned = True
	
	gMoney = 300
	gLives = 20
	
	ClearTowers()
	ClearSpawn()
	ClearEnemies()
	ClearBullets()
	ClearParticles()
	
	gShowBuildMenu = False
	gSelectedTower = 0
	gPlaceTower = False
	
	ClearMouse
EndFunction 

//P‰‰ohjelma
Function Run()
	Repeat
		gLoopStart = Timer()
		
		SetWindow "Money: " + gMoney + "    Lives: " + gLives + "    Current wave: " + gCurrentWave + "/" + gWaveAmount + "    Enemy count: " + gEnemyAmount + "    " + gLoopTime
		
		mX = RoundDown(MouseX() / GRID_SIZE)
		mY = RoundDown(MouseY() / GRID_SIZE)
		
		If gPlaceTower = True	//Tornin asettaminen
			Color 128,0,0
			Box mX * GRID_SIZE,mY * GRID_SIZE,GRID_SIZE,GRID_SIZE
			
			If MouseHit(1) And CheckTile(mX,mY) and gShowBuildMenu = False
				CreateTower(mX,mY,gSelectedTower)
			ElseIf MouseHit(2)
				gPlaceTower = False
				gSelectedTower = 0
			EndIf
		Else	//Tornin valinta
			If MouseHit(1) And GetTowerId(mX,mY) <> 0 And tmpHover = 0
				gSelectedTower = GetTowerId(mX,mY)
			ElseIf MouseHit(2)
				gSelectedTower = 0
			EndIf
		EndIf
		
		If KeyHit(cbKeyB)	//Ostovalikon avaus
			gShowBuildMenu = Not gShowBuildMenu
		ElseIf KeyHit(cbKeySpace) And ((Timer() > gLastSpawn + WAIT_TIME) Or gEnemyAmount = 0)	//Aallon l‰hetys
			gLastSpawn = Timer()
			
			gSpawned = False
			gCurrentWave = gCurrentWave + 1
			
			If gCurrentWave > gWaveAmount
				ClearTowers()
				ClearSpawn()
				ClearEnemies()
				ClearPath()
				
				Return EXIT_SUCCESS
			EndIf
		EndIf
		
		UpdateWaves()
		UpdateEnemies()
		UpdateTowers()
		UpdateBullets()
		
		DrawGrid()
		DrawPath()
		DrawEnemies()
		DrawTowers()
		DrawBullets()
		
		//P‰ivitet‰‰n ja piirret‰‰n partikkelit jos efektit p‰‰ll‰
		If gSettings(SETTING_EFFECTS) = 1
			UpdateParticles()
			DrawParticles()
		EndIf
		
		If gShowBuildMenu = True
			BuildMenu(0,0)
		EndIf
		
		//N‰ytet‰‰n infolaatikko ja otetaan talteen tieto, onko hiiri sen p‰‰ll‰
		If gSelectedTower <> 0 And gPlaceTower = False
			tmpHover = TowerInfo(gSelectedTower)
		Else
			tmpHover = 0
		EndIf
		
		//Infoteksti
		Color 0,Abs(Sin(Timer() / 9)) * 255,0
			
		If (Timer() > gLastSpawn + WAIT_TIME) Or gEnemyAmount = 0
			CenterText gScreenW / 2,GRID_SIZE / 2,"Press SPACE t"+"o spawn n"+"ew wave",2
		Else
			CenterText gScreenW / 2,GRID_SIZE / 2,10 - RoundDown((Timer() - gLastSpawn) / 1000),2
		EndIf
		
		If gLives = 0	//El‰m‰t loppu -> game over
			Return EXIT_GAMEOVER
		ElseIf KeyHit(cbKeyEsc)	//Esc -> pause
			tmp = PauseMenu()
			
			If tmp = False
				Return EXIT_INTERRUPT
			EndIf
		EndIf
		
		DrawScreen

		gLoopTime = (Timer() - gLoopStart)
	Forever
EndFunction

//Asettaa oletusasetukset
Function DefaultSettings()
	gSettings(SETTING_MVOLUME) = 100
	gSettings(SETTING_SVOLUME) = 100
	gSettings(SETTING_EFFECTS) = 1
	gSettings(SETTING_SHUFFLE) = 0
	
	SaveSettings()
EndFunction

//Tallentaa asetukset
Function SaveSettings()
	f = OpenToWrite("data/settings.dat")
		For i = 0 To SETTINGS_AMOUNT - 1
			WriteByte f,gSettings(i)
		Next i
	CloseFile f
EndFunction

//Lataa asetukset
Function LoadSettings()
	If FileExists("data/settings.dat") Then
		f = OpenToRead("data/settings.dat")
			For i = 0 To SETTINGS_AMOUNT - 1
				gSettings(i) = ReadByte(f)
			Next i
		CloseFile f
	EndIf
EndFunction
