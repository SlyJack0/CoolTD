//Alustaa pelin ja lataa kaiken tarvittavan
Function InitGame()
	If FileExists("Data/settings.dat")
		LoadSettings()
	Else
		DefaultSettings()
	EndIf
	
	If gTowerTypeAmount = 0
		LoadTowerData("data/towers.ini")
	EndIf
	
	If gEnemyTypeAmount = 0
		LoadEnemyData("data/enemies.ini")
	EndIf
	
	If gWaveAmount = 0
		LoadWaves("data/waves.txt")
	EndIf
	
	gCurrentWave = 0
	gCurrentGroup = 1
	
	gMoney = 300
	
	LoadPath(1)
	'RandomPath()
EndFunction 

//P‰‰ohjelma
Function Run()
	Repeat
		gLoopStart = Timer()
		
		SetWindow "Money: " + gMoney + "    Lives: " + gLives + "    Current wave: " + gCurrentWave + "/" + gWaveAmount + "    Enemy count: " + gEnemyAmount + "    " + gLoopTime
		
		mX = RoundDown(MouseX() / GRID_SIZE)
		mY = RoundDown(MouseY() / GRID_SIZE)
		
		If gPlaceTower = True
			Color 128,0,0
			Box mX * GRID_SIZE,mY * GRID_SIZE,GRID_SIZE,GRID_SIZE
			
			If MouseHit(1) And CheckTile(mX,mY) and gShowBuildMenu = False
				CreateTower(mX,mY,gSelectedTower)
			ElseIf MouseHit(2)
				gPlaceTower = False
				gSelectedTower = 0
			EndIf
		Else
			If MouseHit(1) And GetTowerId(mX,mY) <> 0
				gSelectedTower = GetTowerId(mX,mY)
			ElseIf MouseHit(2)
				gSelectedTower = 0
			EndIf
		EndIf
		
		If KeyHit(cbKeyB)
			gShowBuildMenu = Not gShowBuildMenu
		ElseIf KeyHit(cbKeySpace) And gEnemyAmount = 0
			gSpawned = False
			gCurrentWave = gCurrentWave + 1
			
			If gCurrentWave > gWaveAmount
				ClearTowers()
				ClearSpawn()
				ClearEnemies()
				ClearPath()
				
				Return EXIT_SUCCESS
			EndIf
		EndIf
		
		UpdateWaves()
		UpdateEnemies()
		UpdateTowers()
		UpdateBullets()
		
		DrawGrid()
		DrawPath()
		DrawEnemies()
		DrawTowers()
		DrawBullets()
		
		//P‰ivitet‰‰n ja piirret‰‰n partikkelit jos efektit p‰‰ll‰
		If gSettings(SETTING_EFFECTS) = 1
			UpdateParticles()
			DrawParticles()
		EndIf
		
		If gShowBuildMenu = True
			BuildMenu(0,0)
		EndIf
		
		If gSelectedTower <> 0 And gPlaceTower = False
			TowerInfo(gSelectedTower)
		EndIf
		
		If gEnemyAmount = 0
			Color 0,Abs(Sin(Timer() / 9)) * 255,0
			CenterText gScreenW / 2,GRID_SIZE / 2,"Press SPACE t"+"o spawn n"+"ew wave",2
		EndIf
		
		If gLives = 0	//El‰m‰t loppu -> game over
			Return EXIT_GAMEOVER
		ElseIf KeyHit(cbKeyEsc)	//Esc -> pause
			tmp = PauseMenu()
			
			If tmp = False
				ClearTowers()
				ClearSpawn()
				ClearEnemies()
				ClearPath()
				
				Return EXIT_INTERRUPT
			EndIf
		EndIf
		
		DrawScreen

		gLoopTime = (Timer() - gLoopStart)
	Forever
EndFunction

//Asettaa oletusasetukset
Function DefaultSettings()
	gSettings(SETTING_MVOLUME) = 100
	gSettings(SETTING_SVOLUME) = 100
	gSettings(SETTING_EFFECTS) = 1
	
	SaveSettings()
EndFunction

//Tallentaa asetukset
Function SaveSettings()
	f = OpenToWrite("Data/settings.dat")
		For i = 0 To SETTINGS_AMOUNT - 1
			WriteByte f,gSettings(i)
		Next i
	CloseFile f
EndFunction

//Lataa asetukset
Function LoadSettings()
	f = OpenToRead("Data/settings.dat")
		For i = 0 To SETTINGS_AMOUNT - 1
			gSettings(i) = ReadByte(f)
		Next i
	CloseFile f
EndFunction
